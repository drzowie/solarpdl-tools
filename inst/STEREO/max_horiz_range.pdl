=head2 max_horiz_range - identify the widest extent of valid imagery in a data processing hash

=for usage

 $xr = max_horiz_range($hash)

=for ref

The input hash should have have the BKSUB_CUBE element.  This routine is normally called from GET_DISTORTION_PARAMS.

=cut

sub max_horiz_range {
    my $hash = shift;
    
    my $badthresh = 50;  # how many DN/sec to be bad?
    my $badcount = 250;  # how many bad pixels allowed in a column before the col is marked bad?
    my $margin = 20;     # how much margin do we leave before the bad cols?

    # Identify (X,t) of columns that are OK -- $okcols is 2-D.
    my $okcols = ($hash->{'BKSUB_CUBE'}->mv(1,0) > $badthresh)->sumover < $badcount;

    # all_ok shows which columns (in X) are OK.
    my $all_ok = $okcols->mv(1,0)->andover;
    
    my $center = $hash->{'BKSUB_CUBE'}->dim(0)/2;
    my $i;

    # Walk out from the center to identify the range.
    for( $i=$center; $all_ok->(($i)); $i++) {}
    my $maxcol = $i - $margin;
    
    for( $i=$center; $all_ok->(($i)); $i--) {}
    my $mincol = $i + $margin;

    if($maxcol - $mincol < 100) {
	die("Not enough good columns in max_horiz_range -- check data quality\n");
    }

    return [$mincol,$maxcol];
}
