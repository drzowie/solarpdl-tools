=head2 remove_polynomials - hi2 pipeline component

=cut
use PDL::Slatec;
use PDL::NiceSlice;

sub remove_polynomials {
    my $hash = shift;
    my $opt = shift;
    my $inst_cube = shift || 0;

    print "into remove_polynomials...\n";

    $opt = {} unless defined($opt);
    
    $opt->{POLY_PRESMOOTH} = 0 unless(defined($opt->{POLY_PRESMOOTH}));
    $opt->{MIN_SMOOTH_SIZE} = 11 unless(defined($opt->{MIN_SMOOTH_SIZE}));
    $opt->{POLY_ORDER} = 3 unless(exists($opt->{POLY_ORDER}));
    $opt->{POLY_CUBE} = "CEL" unless(exists($opt->{POLY_CUBE}));

    my $pc = $opt->{POLY_CUBE}."_CUBE";
    my $cube = $hash->{$pc};
    $n = $cube->dim(2);

    my $c2;
    
    # If we're presmoothing, then presmooth.
    if($opt->{POLY_PRESMOOTH}) {
	print "presmoothing for polynomial fit";
	$c2 = PDL->new_from_specification($cube->mv(2,0)->dims);
	for my $i(0..$c2->dim(0)-1) {
	    $c2->(($i)) .= $cube->(:,:,($i))->med2d(ones($opt->{POLY_PRESMOOTH},$opt->{POLY_PRESMOOTH}));
	    print ".";
	}
	print "\n";
    } else {
	$c2 = $cube->mv(2,0);
    }

    print "fitting polynomials (order=$opt->{POLY_ORDER})...\n";
    
    # Break the polyfit up into pieces to avoid hogging too much temp memory at once
    $pieces = 10;
    print "10 easy pieces...\n";
    for $ii(0..9){
	print "$ii...";
	$min = int($ii * $c2->dim(2)/10);
	$max = int(($ii+1) * $c2->dim(2)/10)-1;
	if($max >= $c2->dim(2)-1) {
	    $max = $c2->dim(2)-1;
	}
	my $c3 = $c2->(:,:,$min:$max);

	my($ndeg, $r, $ierr, $a) = polyfit( xvals($n), $c3, ones(float,$n), $opt->{POLY_ORDER}, $eps );
	undef $ierr;
	undef $a;
	undef $ndeg;
	$c3 -= $r;
    }
    print "\n";

    # accumulate minimum so as not to eat too much memory...
    print "Accumulating minimum....";
    my $ones = ones(float, $opt->{MIN_SMOOTH_SIZE}, $opt->{MIN_SMOOTH_SIZE});
    $min = $cube->(:,:,(0))->med2d( $ones )->(:,:,*2)->copy;
    for $i(1..$cube->dim(2)-1) {
	print "$i...";
	$min->(:,:,(1)) .= $cube->(:,:,($i))->med2d( $ones );
	$min->(:,:,(0)) .= $min->mv(2,0)->minimum;
    }

    print "subtracting minimum smoothed value...\n";
    $cube -= $min->(:,:,(0));
 
    return $hash;
}
