=pod

=head2 frag_tabulate_range

=for ref

Tabulate features in an associated set of frames

=for usage

$tabs = frag_tabulate(\@frag_id_list,\@images,{options});

The output is a hash ref containing a large pdl and several perl
variables about the data set.  The 'summary' field is a pdl indexed by
(feature, frame, data type) and contains the flux, size, and centroid
x and y of each feature in each frame, in that order.  

The IDs get crunched down to a set of consecutive integers.  The
assoc files can be changed in place (see option 'tab'), so if the thing 
crashes you have to remake them, but at least it reduces the egregiousness 
of the disk waste.

OPTIONS

=over 3

=item v_min (default 8)

A feature with a total volume (total number of pixels in the x-y-t sense) 
smaller than this value will get filtered as noise.

=item t_min (default 4)

A feature with a lifetime (in frames) smaller than this value will get 
filtered as noise.

=item sz_min (default 4)

A feature with a maximum size smaller than this value will get filtered as 
noise.

=item tab

Set this option to an array ref or DiskCache object to prevent the assoc 
files from being changed in place.

=item verbose

Chat.

=item start_frame

Frame number to start with

=item DEBUG

Sets verbose option to 2.

=back

BUGS

None at this time (12/31/2007).

HISTORY

Craig DeForest ??? wrote most of it.

Derek Lamb 15-Nov-2004 added reassociation to fix the swiss cheese problem.

Derek Lamb 02-Jun-2005 added documentation.

Derek Lamb 09-Dec-2007 replaced reassociation with the simpler "any
contacts?" check on the filtering.

=cut

use strict;
use PDL;
use PDL::NiceSlice;
use PDL::ImageND;
use DBI;

sub frag_tabulate_range {
    my($opt,$id_list,$im_list) = find_opt(@_);
    local($_);
    
    ##############################
    ## Set up options
    $opt->{v_min} = 8         unless(defined $opt->{v_min});
    $opt->{t_min} = 4         unless(defined $opt->{t_min});
    $opt->{sz_min}= 4         unless(defined $opt->{sz_min});
    $opt->{tab} = $id_list    unless(defined $opt->{tab}); 
    #assoc files are set to change in place unless {tab} is defined
    
#barf if all filtering options are 0; at least one of them needs to be positive
    barf "frag_tabulate called with all filtering options<=0. Refusing to continue." if ($opt->{v_min}<=0 && $opt->{t_min}<=0 && $opt->{sz_min}<=0);

    $opt->{verbose} = 2 if($opt->{DEBUG});
    
    my($start_frame) = $opt->{start_frame} || 0;

    my($dbh) = $opt->{dbh} || undef;
  
    ##############################
    ## Allocate the features table
    
    my($maxfrag) = $id_list->[-1]->abs->max;  
    print "maxfrag is $maxfrag\n";
    print "Allocating ".join("x",$maxfrag+1,scalar(@{$id_list}),4)." floats (".($maxfrag+1)*(scalar(@{$id_list})*4*4/1024/1024)," MB)\n";

    ##  summary is indexed by (feature number, frame number, (flux, size, centroid x, centroid y)) 
    my($summary) = zeroes( float, 1.05 * $maxfrag + 1, scalar(@{$id_list}), 4 );
   
    
    ##############################
    ## Loop over frames and accumulate data.

    for my $fr(0..$#{$id_list}){
	next unless (defined($im_list->[$fr]) && defined($id_list->[$fr]));
	
	print "(1) summarizing f$fr: \n" if($opt->{verbose});
	
	# Retrieve current images & find list of current features
	my($idmap) = abs($id_list->[$fr]);
	my($im) = $im_list->[$fr];
	my($ids) = $idmap->flat->long->uniq;
	
	print $ids->nelem," frags" if($opt->{verbose});
	
	# Loop over current features
	my($id);    
	my($n);
	
	my($idml) = $idmap->long;
	
	## Prepare a single list of all old-ID values and also a list of their coordinates
	my $id_fxy = whichND($idmap != 0);
	my $id_f = $idmap->indexND($id_fxy)->sever;	

	## Loop over old id's and summarize each.
	for $id($ids->list) {
	    next unless $id;  # Skip the null case

   	    # Retrieve loci for summary table.
	    my $xy = $id_fxy->(:,which($id_f==$id));  # 2 x N for feature


	    my($val)       = $im->indexND($xy);
	    my($flux)      = $val->sumover;
	    my($size)      = $xy->dim(1);
	    my($centroid)  = sumover($xy->mv(1,0) * $val) / $flux;
	    
##put in a check: if id is bigger than maxfrag, then we need to append
##a slice to summary. This is an issue if the largest id number in the
##data set isn't alive in the last frame (i.e., very rarely).
	    $summary=$summary->append(zeroes(1,scalar(@{$id_list}),4)) if ($id >= $summary->dim(0));

	    $summary->(($id),($fr),:) .= $flux->append($size)->append($centroid);
	    
	    print "$fr,$id: ",$summary->(($id),($fr),:),", ",$flux->append($size)->append($centroid),"\n"
		if($opt->{verbose}>2);
	    print "." if($opt->{verbose} && (($n++ % 10 == 0) || $opt->{verbose}>1));
	    
	} ##end loop over ids in a frame 

	print "\n" if($opt->{verbose});

    } ##end loop over frames    
    
    ##############################
    ## Filter on volume and other criteria, and crunch IDs.

    print "Filtering features: volume..." if($opt->{verbose});
    my($frag_ok) = ($summary->(:,:,(1))->xchg(0,1)->sumover >= $opt->{v_min});
    
    print "time..." if($opt->{verbose});
    $frag_ok &= (($summary->(:,:,(1))->xchg(0,1) != 0)->sumover) >= $opt->{t_min};
    
    print "size..." if($opt->{verbose});
    $frag_ok &= ($summary->(:,:,(1))->xchg(0,1)->maximum >= $opt->{sz_min});
    
    print "ok\n" if($opt->{verbose});

=pod

if (a feature is filtered){
    if (feature is touching another feature in space or time){
    keep feature
    }
    else{
    kill feature
    }
}

=cut

##note that anything based on which($frag_ok) is only going to have positive ID numbers!
    my $filt_ids = which(!$frag_ok);
########################
#shortcut: if there's no filtering, we can just write out the images and return
    if ($filt_ids->nelem == 1 && $filt_ids->at(0)==0){
	print "shortcutting..." if $opt->{verbose}>1;	
	for my $fr(0..$#{$im_list}) {
	    $opt->{tab}->[$fr] = $id_list->[$fr]->abs;
	}
	print "frag_tabulate completed successfully.";
	return $summary;
    }
#end shortcut
########################

#    my($ok_ids) = which ($frag_ok); #with no reassociation, don't need to make this until later
    
    my $k = ones(3,3);
    my $xsize=$id_list->[0]->dim(0);
  FilteredFeature:    foreach my $feat($filt_ids->list){
      next FilteredFeature if ($feat==0);
      my $frames = which($summary(($feat),:,(1)));
      next unless($frames->nelem);
      my $ft_sgn = $summary(($feat),($frames->at(0)),(0)) <=> 0;
      my $start = $frames->at(0)?$frames->at(0)-1:0;
      my $end = $frames->at(-1)<$summary->dim(1)-1?$frames->at(-1)+1:$summary->dim(1)-1;
      print "Feature $feat lives in frames $frames, also checking $start and $end\n" if ($opt->{verbose}>1);
      
=pod
###check before
      if (($frames->at(0)) &&
	  ($id_list->[$frames->at(0)-1]->where($id_list->[$frames->at(0)]->abs==$feat)->any))
      {
	  $frag_ok(($feat)).=1;
	  next FilteredFeature;
      }
      
###check after
      if (($frames->at(-1)<$summary->dim(1)-1) &&
	  ($id_list->[$frames->at(-1)+1]->where($id_list->[$frames->at(-1)]->abs==$feat)->any))
      {
	  $frag_ok(($feat)).=1;
	  next FilteredFeature;
      }
=cut
      
###check around
      foreach my $fr($frames->list){
	  print " $feat-$fr " if($opt->{verbose}>1);

	  my ($mins,$maxes)=whichND($id_list->[$fr]->abs==$feat)->xchg(0,1)->minmaximum;

	  my $sub_idlist = $id_list->[$fr]->range($mins-2,$maxes-$mins+4,'t');
	  my $dilated = convolveND($sub_idlist == $feat * $ft_sgn, $k)>0;

	  my $localids = $sub_idlist->where($dilated)->uniq;

#localids should at least have $feat in it, and we don't care about 0.
	  if((($localids != $feat*$ft_sgn) & ($localids!=0))->any){
	      $frag_ok(($feat)).=1;
	      next FilteredFeature;
	  }
      }
      
      print "." if($opt->{verbose}==1);
  } #end loop FilteredFeature

 print "\n";    
##need to remake this because we might have re-ok'd some features
    my $ok_ids = which($frag_ok);

    # Generate forward and backward conversions for IDs.  The +1s are to 
    # make the new IDs start at 1 not 0.
    # $id_conv is a hash (old style).
    #
    # CED 24-Jun-2009
    # $idconv is a pdl (new style).
    # $idconv is a lookup table -- index is abs(old_id), which is supposed
    #  to be unique (see frag_assoc), and value is new ID.
    #
    my($a,$id_conv);
    my($idconv) = zeroes(long, $summary->dim(0));
     print "Assigning crunched IDs...\n";
    $idconv->($ok_ids) .= xvals($ok_ids->nelem)+1;
    print "ok\n";

    # Compress all the IDs in the images.
    for my $fr(0..$#{$im_list}) {
	print "(2) crunching f$fr: " if($opt->{verbose});

#	my $frame_ids = $id_list->[$fr]->abs->uniq->(1:-1); #doesn't include 0
#	print $frame_ids->nelem," ids, of which ",intersect($ok_ids,$frame_ids)->nelem," are OK " if $opt->{verbose};

	my $idmap = $idconv->($id_list->[$fr]->abs)->sever;

	$opt->{tab}->[$fr] = $idconv->indexND($id_list->[$fr]->(*1)->abs)->copy;
	print "\n" if($opt->{verbose});
    }
    print "Copying summary...\n";
    my($sum2) = $summary->(append(pdl(0),$ok_ids),:,:)->sever;

    # Write the information to a SQLite database
    my $id;
    my $num_ids;
    my $frame;
    my $num_frames;
    my $flux;
    my $size;
    my $x;
    my $y;

    my $sth = $dbh->prepare('INSERT INTO tabs VALUES (?, ?, ?, ?, ?, ?)');

    $num_ids = $summary->dim(0);
    $num_frames = $summary->dim(1);
    for ($id=0; $id<$num_ids; $id++)
    {
        for ($frame=0; $frame<$num_frames; $frame++)
        {
            $flux = $summary->(($id), ($frame), (0));
            $size = $summary->(($id), ($frame), (1));
            $x = $summary->(($id), ($frame), (2));
            $y = $summary->(($id), ($frame), (3));
            $sth->execute($id, $start_frame + $frame, $flux, $size, $x, $y);
            $sth->finish;
        }
    }

    ## Update the current number of frames in the DB
    $sth = $dbh->prepare('UPDATE params SET cur_num_frames=?');
    $sth->execute($start_frame + $frame);
    $sth->finish;

    print "cleaning up...\n";
    undef $summary;
    print "frag_tabulate completed successfully.\n";
    return $sum2;
}
