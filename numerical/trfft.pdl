=head2 trfft - threading realfft

=for ref

Threading fft - variable number of active dims.  inplace-flag aware.
By default there is one active dim that is transformed over.
You can set how many additional dims are transformed, or specify that
all them should be, using an options hash.

The real part of the transform goes in the first half of the array,
and the imaginary part goes in the second half.

See also C<tfft>.

Accepted options are:

=over 3

=item inverse (default 0)

This flags an inverse transform.  You can also get this behavior
by calling ifft instead of fft.

=item ndims (default 1)

This indicates the number of dimensions you want transformed.
If you feed in 0, then *all* data dimensions are transformed over.
(see also "all",  below)

=item all (default 0)

This flag, if true, overrides C<ndims> and transforms over all
relevant dimensions.

=back

=cut
use PDL::Options;
use PDL::FFT qw/:Func/;

sub trfft {
    my $data = shift->new_or_inplace;
    my $u_opt = shift // {};
    my $opt = parse(
	{
	    inverse => 0,
	    ndims   => 1,
	    all     => 0
	},
	$u_opt
	);
    
    if($data->dims < 1 ){
	die "fft: data must have at least one dim.";
    }

    if($opt->{dims} == 0 or  $opt->{all} == 1 ) {
	$opt->{dims} = $data->ndims;
    }
    unless($opt->{dims} > 0 && $opt->{dims} <= $data->ndims) {
	die "tfft: Dim count out of range";
    }

    for my $i(0..$opt->{dims}-1) {
	if($opt->{inverse}) {
	    realifft($data->mv($i,0));
	} else {
	    realfft($data->mv($i,0));
	}
    }

    return $data;
}

